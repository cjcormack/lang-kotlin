buildscript {
    ext {
        versions = [
                "kotlin": '1.3.50'
        ]
    }

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
        maven {
            url "http://maven.netkernel.org/netkernel-maven"
        }
    }

    dependencies {
        classpath group: 'urn.org.netkernel', name: 'gradle.plugin', version: '[1.1.4,)'
    }
}

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.dokka' version '0.9.18'
}
repositories {
    jcenter() // or maven { url 'https://dl.bintray.com/kotlin/dokka' }
}

apply plugin: 'netkernel'

def nk_path=project.properties['nk_path'] ?: "../NKSE-5.2.1/"

netkernel {
    useMavenCentral()
    useMavenNK()

    instances {
        NK {
            edition = "SE"
            location = nk_path
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/${netkernel.module.name}/dokka"
}

compileKotlin {
    kotlinOptions{
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
    }
}

//Additional Compile Dependencies
dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-compiler:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-script-util:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-scripting-jvm-host:${versions.kotlin}"

  compileOnly group: 'urn.org.netkernel', name: 'mod.hds', version: '[1.5.1,)'
}


task copyToLib(type: Copy) {
    into "$buildDir/${netkernel.module.name}/lib"
    from configurations.runtime {}
}

jar.dependsOn(copyToLib, dokka)
