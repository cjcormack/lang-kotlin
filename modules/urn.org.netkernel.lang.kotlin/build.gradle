plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.dokka' version '1.7.20'
}

apply plugin: 'netkernel'

sourceSets["main"].java.srcDirs("src")
sourceSets["main"].kotlin.srcDirs("src")
sourceSets["main"].resources.srcDirs("src")

sourceSets["main"].java.exclude("**/*.kts")
sourceSets["main"].kotlin.exclude("**/*.kts")

def nk_path = project.properties['nk_path'] ?: "../NKSE-5.2.1/"

netkernel {
    useMavenCentral()
    useMavenNK()

    instances {
        NK {
            edition = "SE"
            location = nk_path
        }
    }
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/${netkernel.module.name}/dokka"))
}

//Additional Compile Dependencies
dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-compiler:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-script-util:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-scripting-jvm-host:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-script-runtime:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-scripting-compiler:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:${versions.kotlin}"

  compileOnly group: 'urn.org.netkernel', name: 'mod.hds', version: '[1.5.1,)'
  compileOnly group: 'urn.org.netkernel', name: 'ext.layer1', version: '[1.60.26,)'
}

jar.dependsOn(dokkaHtml)

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

// Pin target JVM to Java 11
sourceCompatibility = 11
targetCompatibility = 11
