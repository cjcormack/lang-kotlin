plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'maven-publish'
}

apply plugin: 'netkernel'
apply plugin: 'maven-publish'

sourceSets["main"].java.srcDirs("src")
sourceSets["main"].kotlin.srcDirs("src")
sourceSets["main"].resources.srcDirs("src")

sourceSets["main"].java.exclude("**/*.kts")
sourceSets["main"].kotlin.exclude("**/*.kts")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // tweak the generated pom to not include dependencies
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

def nk_path = project.properties['nk_path'] ?: "../NKSE-5.2.1/"

netkernel {
    useMavenCentral()
    useMavenNK()

    instances {
        NK {
            edition = "SE"
            location = nk_path
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/${netkernel.module.name}/dokka"
}

//Additional Compile Dependencies
dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-compiler:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-script-util:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-scripting-jvm-host:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-script-runtime:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-scripting-compiler:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:${versions.kotlin}"

  compileOnly group: 'urn.org.netkernel', name: 'mod.hds', version: '[1.5.1,)'
}

jar.dependsOn(dokka)
